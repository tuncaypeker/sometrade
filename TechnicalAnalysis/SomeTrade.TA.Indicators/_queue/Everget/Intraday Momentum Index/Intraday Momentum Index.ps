//@version=4
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Intraday Momentum Index script may be freely distributed under the MIT license.
study("Intraday Momentum Index", shorttitle="IMI")

length = input(title="Length", type=input.integer, minval=1, defval=14)
highlightBreakouts = input(title="Highlight Overbought/Oversold Breakouts ?", type=input.bool, defval=true)

gain = close > open ? close - open : 0
loss = close > open ? 0 : open - close

upSum = sum(gain, length)
downSum = sum(loss, length)

imi = 100 * upSum / (upSum + downSum)

obLevel = 70
osLevel = 30

imiColor = imi > obLevel ? #0ebb23 : imi < osLevel ? #ff0000 : #f4b77d
plot(imi, title="IMI", linewidth=2, color=imiColor, transp=0)

transparent = color.new(color.white, 100)

maxLevelPlot = hline(100, title="Max Level", linestyle=hline.style_dotted, color=transparent)
obLevelPlot = hline(obLevel, title="Overbought Level", linestyle=hline.style_dotted)
hline(50, title="Middle Level", linestyle=hline.style_dotted)
osLevelPlot = hline(osLevel, title="Oversold Level", linestyle=hline.style_dotted)
minLevelPlot = hline(0, title="Min Level", linestyle=hline.style_dotted, color=transparent)

fill(obLevelPlot, osLevelPlot, color=color.purple, transp=95)

obFillColor = imi > obLevel and highlightBreakouts ? color.green : transparent
osFillColor = imi < osLevel and highlightBreakouts ? color.red : transparent

fill(maxLevelPlot, obLevelPlot, color=obFillColor, transp=90)
fill(minLevelPlot, osLevelPlot, color=osFillColor, transp=90)