//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Price Momentum Oscillator script may be freely distributed under the MIT license.
study("Price Momentum Oscillator", shorttitle="PMO")

firstLength = input(title="1st Smoothing Length", type=integer, defval=35)
secondLength = input(title="2nd Smoothing Length", type=integer, defval=20)
signalLength = input(title="Signal Length", type=integer, defval=10)
src = input(title="Source", type=source, defval=close)
highlightCrossovers = input(title="Highlight PMO/Signal Crossovers ?", type=bool, defval=false)
highlightZeroCrossovers = input(title="Highlight Zero Line Crossovers ?", type=bool, defval=false)
applyFilling = input(title="Apply Ribbon Filling ?", type=bool, defval=false)

pmo = ema(10 * ema(nz(roc(src, 1)), firstLength), secondLength)
signal = ema(pmo, signalLength)

trendColor = pmo > signal ? #0ebb23 : red

pmoColor = applyFilling ? trendColor : #0094ff
signalColor = applyFilling ? trendColor : #ff6a00

pmoPlot = plot(pmo, title="PMO", color=pmoColor, transp=0)
signalPlot = plot(signal, title="Signal", color=signalColor, transp=0)
hline(0, title="Zero Line", linestyle=dotted)

transparent = color(white, 100)

fillColor = applyFilling ? trendColor : transparent
fill(pmoPlot, signalPlot, color=fillColor, transp=70)

zeroCrossBgColor = highlightZeroCrossovers ? (pmo > 0 ? green : red) : transparent
bgcolor(zeroCrossBgColor, transp=90)

plotshape(crossover(pmo, signal) and highlightCrossovers ? pmo : na, title="Crossover", location=location.absolute, style=shape.circle, size=size.tiny, color=green, transp=0)
plotshape(crossunder(pmo, signal) and highlightCrossovers ? pmo : na, title="Crossunder", location=location.absolute, style=shape.circle, size=size.tiny, color=red, transp=0)